pkgname=agent-browser
pkgver=0.13.0
pkgrel=1
_node_ver=22.22.0
pkgdesc="Headless browser automation CLI for AI agents"
arch=('x86_64')
url="https://github.com/vercel-labs/agent-browser"
license=('Apache-2.0')
depends=('nodejs')
install=agent-browser.install
source=(
  "${pkgname}-${pkgver}.tgz::https://registry.npmjs.org/${pkgname}/-/${pkgname}-${pkgver}.tgz"
  "node-v${_node_ver}-linux-x64.tar.xz::https://nodejs.org/dist/v${_node_ver}/node-v${_node_ver}-linux-x64.tar.xz"
)
sha256sums=('c57a35aeeb5a5792480ffbf40c3f8cb824c054de5d16fa34bc571c2a40f7083c'
            '9aa8e9d2298ab68c600bd6fb86a6c13bce11a4eca1ba9b39d79fa021755d7c37')

package() {
  local _node_bin="${srcdir}/node-v${_node_ver}-linux-x64/bin"

  # Use upstream Node binary so packaging does not depend on host glibc/node ABI.
  export PATH="${_node_bin}:${PATH}"
  export PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD=1

  npm install -g --prefix "${pkgdir}/usr" "${srcdir}/${pkgname}-${pkgver}.tgz"
  install -Dm644 "${pkgdir}/usr/lib/node_modules/${pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"

  local _arch_suffix
  case "${CARCH}" in
    x86_64) _arch_suffix='x64' ;;
    aarch64) _arch_suffix='arm64' ;;
    *) _arch_suffix="${CARCH}" ;;
  esac

  chmod 755 "${pkgdir}/usr/lib/node_modules/${pkgname}/bin/agent-browser-linux-${_arch_suffix}"
}
