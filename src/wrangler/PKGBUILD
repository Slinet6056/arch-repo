pkgname=wrangler
pkgver=4.67.0
pkgrel=1
_esbuild_ver='0.27.3'
_node_ver='22.14.0'
pkgdesc="The CLI for Cloudflare Workers"
url='https://github.com/cloudflare/workers-sdk'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
  'nodejs'
  'worker-build'
  'workerd'
)
makedepends=(
  'go'
  'npm'
  'python'
  'pnpm'
  'turbo'
  'typescript'
  'yq'
)
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/cloudflare/workers-sdk/archive/refs/tags/wrangler@${pkgver}.tar.gz
  "esbuild-${_esbuild_ver}.tar.gz::https://github.com/evanw/esbuild/archive/v${_esbuild_ver}.tar.gz"
  "node-v${_node_ver}-linux-x64.tar.xz::https://nodejs.org/dist/v${_node_ver}/node-v${_node_ver}-linux-x64.tar.xz")
sha256sums=('f1601fcbbf18010276cdc70be36fe21ae2453914ce1d98d89e39c8e7140e9c98'
            '05d56070104b46d24c8921bfc4c83209d71cf583eb0396c13d0f359705bb5b61'
            '69b09dba5c8dcb05c4e4273a4340db1005abeafe3927efda2bc5b249e80437ec')
b2sums=('c2f4e50a0c17d6aa461fc29be078602700db1545e44790ae9b98369a7c916094c595e08e43ef1efb3c0c6b705af25b96e142286df7acd6a8ca2b0b44f4d9c12b'
        'b9a3f3d5c573e64e983d87d49de4b677aaab3f71d5c3dd32b22956833c407129a82b7caf4b2b5025dae95d56edec42acbf4f8f61c0b6860d0c29ee1e007c448e'
        'c51c1343fd6cbc3a4c0f4291543c1573e036d72d413531d79d29be53f526f9ceefc0c2e290211fb05cbea3a5e2b7a246c7cce65c383c69f55ea7dbd5b49b93fa')

prepare() {
  cd "workers-sdk-wrangler-${pkgver}"

  # verify we're still using the correct esbuild version
  esbuild=$(yq -r .catalogs.default.esbuild.version pnpm-lock.yaml)
  [[ "$esbuild" == "$_esbuild_ver" ]]

  # @hey-api/openapi-ts 0.91.1 misses plugin bundle files in npm package.
  # Patch local-explorer-ui scripts with a minimal dist stub for packaging.
  python <<'PY'
import json
from pathlib import Path

path = Path('packages/local-explorer-ui/package.json')
pkg = json.loads(path.read_text(encoding='utf-8'))

pkg['scripts']['postinstall'] = 'true'
pkg['scripts']['build'] = (
    "node -e \"const fs=require('node:fs'); "
    "fs.mkdirSync('dist',{recursive:true}); "
    "fs.writeFileSync('dist/index.html','<!doctype html><html><body></body></html>\\n');\""
)

path.write_text(json.dumps(pkg, indent='\t') + '\n', encoding='utf-8')
PY

}

build() {
  cd "workers-sdk-wrangler-${pkgver}"

  local _node_bin="${srcdir}/node-v${_node_ver}-linux-x64/bin"
  # Use upstream Node binary to avoid host glibc/node ABI mismatches in CI.
  export PATH="${_node_bin}:${PATH}"

  # Avoid workspace postinstall hooks (e.g. local-explorer-ui openapi-ts)
  # that are not required to build/publish wrangler itself.
  export npm_config_ignore_scripts=true

  pnpm install -C packages/wrangler --frozen-lockfile --ignore-scripts
  # Build wrangler and required workspace dependencies.
  turbo build --filter=wrangler...
  pnpm pack -C packages/wrangler

  # build a copy of the specific esbuild version wrangler depends on
  # getting this wrong makes `wrangler dev` fail with no error
  cd "${srcdir}/esbuild-${_esbuild_ver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/esbuild
}

package() {
  npm install -g --prefix "${pkgdir}/usr" "workers-sdk-wrangler-${pkgver}/packages/wrangler/wrangler-${pkgver}.tgz"
  install -Dm644 "workers-sdk-wrangler-${pkgver}/LICENSE-MIT" -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # This file is very big
  rm "${pkgdir}/usr/lib/node_modules/wrangler/wrangler-dist/cli.js.map"

  # Remove pre-compiled binaries
  rm -rv \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/esbuild" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/esbuild"

  # Add symlinks to workerd package
  ln -s "../../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/"
  ln -s "../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/"

  # Add esbuild binary
  install -Dm755 -t "${pkgdir}/usr/lib/wrangler/bin/" \
    "esbuild-${_esbuild_ver}/esbuild"
  ln -s "../../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/"
  ln -s "../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/"
}

# vim: ts=2 sw=2 et:
