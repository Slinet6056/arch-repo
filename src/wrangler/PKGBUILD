pkgname=wrangler
pkgver=4.66.0
pkgrel=1
_esbuild_ver='0.27.3'
pkgdesc="The CLI for Cloudflare Workers"
url='https://github.com/cloudflare/workers-sdk'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
  'nodejs'
  'worker-build'
  'workerd'
)
makedepends=(
  'go'
  'npm'
  'pnpm'
  'turbo'
  'typescript'
  'yq'
)
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/cloudflare/workers-sdk/archive/refs/tags/wrangler@${pkgver}.tar.gz
  "esbuild-${_esbuild_ver}.tar.gz::https://github.com/evanw/esbuild/archive/v${_esbuild_ver}.tar.gz")
sha256sums=('288e779bf3f43023974e07962ef6ac74fd6d5e947d165ca39c85cf08f0a49032'
            '05d56070104b46d24c8921bfc4c83209d71cf583eb0396c13d0f359705bb5b61')
b2sums=('13666465849a78d614d5988171c22f50be2270761f2c7314bebef62b1453cb99533fa96700961730fd1eb3b352a50a6084800e25cac514d32e66c0b22b5e07f8'
        'b9a3f3d5c573e64e983d87d49de4b677aaab3f71d5c3dd32b22956833c407129a82b7caf4b2b5025dae95d56edec42acbf4f8f61c0b6860d0c29ee1e007c448e')

prepare() {
  cd "workers-sdk-wrangler-${pkgver}"

  # verify we're still using the correct esbuild version
  esbuild=$(yq -r .catalogs.default.esbuild.version pnpm-lock.yaml)
  [[ "$esbuild" == "$_esbuild_ver" ]]

  # @hey-api/openapi-ts 0.91.1 misses plugin bundle files in npm package.
  # Patch local-explorer-ui scripts with a minimal dist stub for packaging.
  node <<'NODE'
const fs = require('node:fs');
const path = 'packages/local-explorer-ui/package.json';
const pkg = JSON.parse(fs.readFileSync(path, 'utf8'));

pkg.scripts.postinstall = 'true';
pkg.scripts.build = 'node -e "const fs=require(\'node:fs\'); fs.mkdirSync(\'dist\',{recursive:true}); fs.writeFileSync(\'dist/index.html\',\'<!doctype html><html><body></body></html>\\n\');"';

fs.writeFileSync(path, `${JSON.stringify(pkg, null, '\t')}\n`);
NODE

}

build() {
  cd "workers-sdk-wrangler-${pkgver}"

  # Avoid workspace postinstall hooks (e.g. local-explorer-ui openapi-ts)
  # that are not required to build/publish wrangler itself.
  export npm_config_ignore_scripts=true

  pnpm install -C packages/wrangler --frozen-lockfile --ignore-scripts
  # Build wrangler and required workspace dependencies.
  turbo build --filter=wrangler...
  pnpm pack -C packages/wrangler

  # build a copy of the specific esbuild version wrangler depends on
  # getting this wrong makes `wrangler dev` fail with no error
  cd "${srcdir}/esbuild-${_esbuild_ver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/esbuild
}

package() {
  npm install -g --prefix "${pkgdir}/usr" "workers-sdk-wrangler-${pkgver}/packages/wrangler/wrangler-${pkgver}.tgz"
  install -Dm644 "workers-sdk-wrangler-${pkgver}/LICENSE-MIT" -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # This file is very big
  rm "${pkgdir}/usr/lib/node_modules/wrangler/wrangler-dist/cli.js.map"

  # Remove pre-compiled binaries
  rm -rv \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/esbuild" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/esbuild"

  # Add symlinks to workerd package
  ln -s "../../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/"
  ln -s "../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/"

  # Add esbuild binary
  install -Dm755 -t "${pkgdir}/usr/lib/wrangler/bin/" \
    "esbuild-${_esbuild_ver}/esbuild"
  ln -s "../../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/"
  ln -s "../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/"
}

# vim: ts=2 sw=2 et:
